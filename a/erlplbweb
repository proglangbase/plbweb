#!/usr/bin/env escript
%%
%% PLB web service node controller.
%% 
%%   1) Change working directory to `code/erlang`.
%%   2) Rebuild any out of date `.beam` files for the service and dependencies.
%%   3) Process the command.
%% 
-mode(compile).
-include("../dep/plbcom/code/erlang/config.hrl").

main([Cmd]) ->
  CodePath = filename:absname(?MODULE_PATH ++ "/../code/erlang"),
  ComPath  = filename:absname(?MODULE_PATH ++ "/../dep/plbcom/code/erlang"),
  Modules  = [ ComPath++"/*", CodePath++"/*" ],
  file:set_cwd(CodePath),
  make:all([{emake, Modules}]),
  c:l(plb_com),
  try
    plb_ctl:init(),
    case list_to_atom(string:lowercase(Cmd)) of
      start   -> start();
      stop    -> stop();
      restart -> restart();
      status  -> status();
      _       -> main([])
    end,
    halt(0)
  catch
    {error,  Info} -> plb_ctl:report(io_lib:format("error: ~p", [Info])),     halt(1);
    {Source, Msg}  -> plb_ctl:report(io_lib:format("~p: ~s", [Source, Msg])), halt(1)
  end;

main(_) -> 
  io:format("usage: ~s {start|start_perl|stop|restart|status}~n", 
            [filename:basename(escript:script_name())]).

start() ->
  case plb_ctl:start_node(?WEB_NODE_NAME, ?WEB_NODE_COOKIE) of
    already_running ->
      plb_ctl:report(?WEB_NODE_SNAME ++ " already running");
    ok ->
      plb_ctl:rpc(?WEB_NODE_NAME, plbwebinets, acquire,  []),
      status()
  end.

stop() ->
  plb_ctl:stop_node(?WEB_NODE_NAME, ?WEB_NODE_COOKIE),
  status().

restart() ->
  case plb_ctl:is_node_running(?WEB_NODE_NAME) of
    true -> stop();
    _ -> ok
  end,
  start().

status() ->
  case plb_ctl:is_node_running(?WEB_NODE_NAME) of
    true  -> plb_ctl:report(?WEB_NODE_SNAME ++ " is running");
    false -> plb_ctl:report(?WEB_NODE_SNAME ++ " not running")
  end.
